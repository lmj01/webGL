<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8">
<script id="vs" type="x-shader/x-vertex">
#version 300 es

precision highp float;
precision highp int;

uniform mat4 umv;
layout(location = 0) in vec3 position;
void main()
{
    gl_Position = umv * vec4(position, 1.0);
}
</script>
<script id="fs" type="x-shader/x-fragment">
#version 300 es
precision highp float;
precision highp int;
precision highp sampler2DArray;

const int maxSamples = 256;

const vec3 uBBMin = vec3(0, 0, 0);
const vec3 uBBMax = vec3(1, 1, 1);

const float uBrightness = -0.117;
const float uContrast = 1.0;
const float uSaturation = 0.0;
const float uPower = 1.0;

const int uSamples = 1024;
const float uDensityFactor = 5.0;
const float uIsoValue = 0.45;
const vec4 uIsoColour = vec4(1.0, 1.0, 0.8, 1.0);
const float uIsoSmooth = 0.5;
const int uIsoWalls = 1;
const vec2 uRange = vec2(0.0, 1.0);
const vec2 uDenMinMax = vec2(0.0, 1.0);

uniform sampler2DArray uvolume;

uniform mat4 umv; // modelview matrix 
uniform mat4 uproject; // projection matrix 
uniform mat4 uiproject; // invert projection matrix 
uniform mat4 unormal; // normal matrix 
uniform vec4 uvp; // viewport
uniform vec3 uResolution; 

float tex3D(vec3 pos) {
    vec3 v = texture(uvolume, pos).xyz;
    return dot(v, v);
}

// light moves with camera
const vec3 lightPos = vec3(0.5, 0.5, 5.0);
const float ambient = 0.2;
const float diffuse = 0.8;
const vec3 diffColur = vec3(1.0, 1.0, 1.0);
const vec3 ambColour = vec3(0.2, 0.2, 0.2);

void lighting(in vec3 pos, in vec3 normal, inout vec3 colour) {
    vec4 vertPos = umv * vec4(pos, 1.0);
    vec3 lightDir = normalize(lightPos - vertPos.xyz);
    vec3 lightWeighting = ambColour + diffColur * diffuse * 
        clamp(abs(dot(normal, lightDir)), 0.1, 1.0);
    colour *= lightWeighting;
}

vec3 isoNormal(in vec3 pos, in vec3 shift, in float density) {
    vec3 shiftpos = vec3(pos.x + shift.x, pos.y + shift.y, pos.z + shift.z);
    vec3 shiftx = vec3(shiftpos.x, pos.y, pos.z);
    vec3 shifty = vec3(pos.x, shiftpos.y, pos.z);
    vec3 shiftz = vec3(pos.x, pos.y, shiftpos.z);

    // detect bounding box hit(walls)
    // -1.0 to 1.0
    if (uIsoWalls > 0) {
        if (pos.x <= uBBMin.x) return vec3(-1.0, 0.0, 0.0);
        if (pos.x >= uBBMax.x) return vec3(1.0, 0.0, 0.0);
        if (pos.y <= uBBMin.y) return vec3(0.0, -1.0, 0.0);
        if (pos.y >= uBBMax.y) return vec3(0.0, 1.0, 0.0);
        if (pos.z <= uBBMin.z) return vec3(0.0, 0.0, -1.0);
        if (pos.z >= uBBMax.z) return vec3(0.0, 0.0, 1.0);
    }

    // calculate normal 
    return vec3(density) - vec3(tex3D(shiftx), tex3D(shifty), tex3D(shiftz));
}

vec2 rayIntersectBox(vec3 rayDirection, vec3 rayOrigin) {
    // intersect ray with bounding-box 
    vec3 rayInvDirection = 1.0 / rayDirection;
    vec3 bbMinDiff = (uBBMin - rayOrigin) * rayInvDirection;
    vec3 bbMaxDiff = (uBBMax - rayOrigin) * rayInvDirection;
    vec3 imax = max(bbMaxDiff, bbMinDiff);
    vec3 imin = min(bbMaxDiff, bbMinDiff);
    // make sure in cube box 
    float back = min(imax.x, min(imax.y, imax.z));
    float front = max(max(imin.x, 0.0), max(imin.y, imin.z));
    return vec2(back, front);
}
out vec4 color;

void main()
{
    // compute eye space coord from window space to get the ray direction 
    mat4 imv = transpose(umv);
    // objectspace * [mv] = eyespace * [p] = clip / w = normalised device coords -> vp -> window 
    // window -> [vp^] -> ndc -> [/w] -> clip -> [p^] -> eyespace -> [mv^] ->objectspace
    vec4 ndcPos;
    ndcPos.xy = ((2.0 * gl_FragCoord.xy) - (2.0 * uvp.xy)) / (uvp.zw) - 1.0;
    ndcPos.z = (2.0 * gl_FragCoord.z - gl_DepthRange.near - gl_DepthRange.far) /
               (gl_DepthRange.far - gl_DepthRange.near);
    ndcPos.w = 1.0;
    vec4 clipPos = ndcPos / gl_FragCoord.w;
    vec3 rayDirection = normalize((imv * uiproject * clipPos).xyz);

    // ray origin from the camera position 
    vec4 camPos = -vec4(umv[3]); // 4th column of modelview 
    vec3 rayOrigin = (imv * camPos).xyz;

    //calc step 
    float stepSize = 1.732 / float(uSamples); // diagonal of [0,1] normalized coord cube = sqrt(3);

    // Intersect ray with bounding box 
    vec2 intersection = rayIntersectBox(rayDirection, rayOrigin);
    // subtract small increment to avoid error on front boundary 
    intersection.y -= 0.000001;
    // discard points outside the box(no intersection)
    // !!!
    if (intersection.x <= intersection.y) discard;

    vec3 rayStart = rayOrigin + rayDirection * intersection.y;
    vec3 rayStop = rayOrigin + rayDirection * intersection.x;

    vec3 step = normalize(rayStop - rayStart) * stepSize;
    vec3 pos = rayStart;

    float T = 1.0;
    vec3 colour = vec3(0.0);
    bool inside = false;
    vec3 shift = 1.0 / uResolution;
    // number of samples to take along this ray before we pass out back of volume 
    float travel = distance(rayStop, rayStart) / stepSize;
    int samples = int(ceil(travel));
    float range = uRange.y - uRange.x;
    if (range <= 0.0) range = 1.0;
    // scale isovalue 
    float isoValue = uRange.x + uIsoValue * range;

    // raymarch, front to back 
    for (int i=0; i < maxSamples; i++) {
        // render samples until we pass out back of cube or fully opaque 
        if (i == samples || T < 0.01) break;
        {
            // get density 
            float density = tex3D(pos);

            // passed through isosurface?
            if (isoValue > uRange.x && ((!inside && density >= isoValue) || (inside && density < isoValue)))
            {
                inside = !inside;
                //Find closer to exact position by iteration
                //http://sizecoding.blogspot.com.au/2008/08/isosurfaces-in-glsl.html
                float exact;
                float a = intersection.y + (float(i) * stepSize);
                float b = a - stepSize;
                for (int j=0; j<5; j++) {
                    exact = (b + a) * 0.5;
                    pos = rayDirection * exact + rayOrigin;
                    density = tex3D(pos);
                    if (density - isoValue < 0.0)
                        b = exact;
                    else 
                        a = exact;
                }

                // skip edges unless flagged to draw 
                if (uIsoWalls > 0 || all(greaterThanEqual(pos, uBBMin)) && all(lessThanEqual(pos, uBBMax))) {
                    vec4 value = vec4(uIsoColour.rgb, 1.0);
                    vec3 normal = normalize(mat3(unormal) * isoNormal(pos, shift, density));
                    vec3 light = value.rgb;
                    lighting(pos, normal, light);
                    // front-to-back blend equation 
                    colour += T * uIsoColour.a * light;
                    T *= (1.0 - uIsoColour.a);
                }
            }

            if (uDensityFactor > 0.0) {
                // normalize the density over provided range 
                density = (density - uRange.x) / range;
                density = clamp(density, 0.0, 1.0);
                if (density < uDenMinMax[0] || density > uDenMinMax[1]) {
                    // skip to next sample 
                    pos += step;
                    continue;
                }

                density = pow(density, uPower); // apply power 

                vec4 value = vec4(density);
                value *= uDensityFactor * stepSize;
                
                // color and alpha 
                colour += T * value.rgb;
                T *= 1.0 - value.a;
            }
        }
        
        // next sample 
        pos += step;
    }

    // apply uBrightness, saturation & contrast 
    colour += uBrightness;
    const vec3 LumCoeff = vec3(0.2125, 0.7154, 0.0721);
    vec3 AvgLumin = vec3(0.5, 0.5, 0.5);
    vec3 intensity = vec3(dot(colour, LumCoeff));
    colour = mix(intensity, colour, uSaturation);
    colour = mix(AvgLumin, colour, uContrast);

    if (T > 0.95) discard;
    color = vec4(colour, 1.0 - T);

    // wirte depth 
    if (true) {             
        vec4 clip_space_pos = uproject * umv * vec4(rayStart, 1.0);
        float ndc_depth = clip_space_pos.z / clip_space_pos.w;
        float depth = (((gl_DepthRange.far - gl_DepthRange.near) * ndc_depth) +
                        gl_DepthRange.near + gl_DepthRange.far) / 2.0;
        gl_FragDepth = depth;
    }
}
</script>
<script id="line-vs" type="x-shader/x-vertex">
#version 300 es
precision highp float;

layout(location = 0) in vec3 aVertexPosition;
layout(location = 1) in vec4 aVertexColour;

uniform mat4 umv;
uniform mat4 uproject;

uniform vec4 uColour;
uniform float uAlpha;

out vec4 vColour;

void main(void)
{
    vec4 mvPosition = umv * vec4(aVertexPosition, 1.0);
    gl_Position = uproject * mvPosition;
    vec4 colour = aVertexColour;
    float alpha = 1.0;
    if (uColour.a > 0.01) colour = uColour;
    if (uAlpha > 0.01) alpha = uAlpha;
    vColour = vec4(colour.rgb, colour.a * alpha);
}
</script>

<script id="line-fs" type="x-shader/x-fragment">
#version 300 es
precision highp float;

in vec4 vColour;
out vec4 color;

void main(void)
{
    color = vColour;
}
</script>
<style>
    button {
        position: relative;
        z-index: 5;
    }
    #demo {
        position: absolute;
        border: 1px solid red;
        width: 100%;
        height: 100%;
    }    
</style>
<script src="./gl-matrix.js"></script>
<script src="./signals.js"></script>
<script src="./auxiliary.js"></script>
</head>
<body>
    <canvas id="demo" ></canvas>
    <button onclick="rotateX();">rotate-x</button>
    <button onclick="rotateY();">rotate-y</button>
    <button onclick="rotateZ();">rotate-z</button>
    <button onclick="rotateXY();">rotate-xy</button>   
    <script>
        let def_width = 256;
        let def_height = 256;
        let def_depth = 256;
        let def_slicex = 16;
        let def_slicey = 16;
        let no_def = false;
        let width = no_def ? 400 : def_width;
        let height = no_def ? 400 : def_height;
        let depth = no_def ? 272 : def_depth;
        let slicex = no_def ? 16 : def_slicex;
        let slicey = no_def ? 17 : def_slicey;
        let myevent = {
            loadtex: new signals.Signal()
        }
        let canvas = document.getElementById('demo');
        // let canvasWidth = 800;
        // let canvasHeight = 600;
        // canvas.width = canvasWidth;
        // canvas.height = canvasHeight;
        // canvas.setAttribute("width", canvasWidth);
        // canvas.setAttribute("Height", canvasHeight);
        let gl = canvas.getContext( 'webgl2', { antialias: true } )
        if( gl == null ) { alert( 'requires webgl2 support!' ) }

        var render = new mqRender(gl, {
            width: canvas.width, 
            height: canvas.height,
            dimx: width,
            dimy: height,
            dimz: depth,
            slicex: slicex, 
            slicey: slicey,
        });
        var glsl = render.glsl;
        myevent.loadtex.add(work_render);

        if (no_def) {
            loadImage('/texture3d/test272.jpeg', function(img) {
                render.loadTextureArray(img, width, slicex, slicey);
                myevent.loadtex.dispatch(slicex * slicey);
            });
        } else {
            loadImage('/texture3d/data.jpg', function(img) {
                render.loadTextureArray(img, width, slicex, slicey);
                myevent.loadtex.dispatch(slicex * slicey);
            });
        }
                
        function work_render(totalFrame) {  
            
            render.initDrawVolume();
            
            var degree = 0;
            (function anonymousRender() {
                
                render.draw();
                
                setTimeout( function(){
                    requestAnimationFrame(anonymousRender);
                    // render.rotateX(degree);
                    // render.rotateY(degree);
                    // render.rotateZ(degree);
                    degree++;
                    if (degree > 360) degree = 0;
                }, 200);
            })();
        }

        function rotateX() {
            render.rotateX(10);
        }
        function rotateY() {
            render.rotateY(10);
        }
        function rotateXY() {
            render.rotateX(10);
            render.rotateY(10);
        }
        function rotateZ() {
            render.rotateZ(10);
        }
    
    </script>        
</body>
</html>